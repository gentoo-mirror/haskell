# Copyright 1999-2023 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

# ebuild generated by hackport 0.8.0.0.9999
#hackport: flags: -examples

CABAL_HACKAGE_REVISION=3

# Give it a different version number so we don't get collisions
CABAL_PV="0.8.2.0.1"

CABAL_FEATURES="lib profile haddock hoogle hscolour" # test-suite
inherit haskell-cabal

# Make sure these get reset to their original values
S="${WORKDIR}/${CABAL_PN}-${PV}"
CABAL_FILE="${S}/${CABAL_PN}.cabal"

DESCRIPTION="A command-line interface for user input, written in Haskell"
HOMEPAGE="https://github.com/judah/haskeline"

LICENSE="BSD"
SLOT="0/${PV}"
KEYWORDS="~amd64 ~x86 ~amd64-linux ~x86-linux"
IUSE="+terminfo"

# The new version needs to match CABAL_PV
CABAL_CHDEPS=(
	'Version:        0.8.2' 'Version:        0.8.2.0.1'
)

RESTRICT=test # test requires example executable which does not work correctly

RDEPEND="
	>=dev-haskell/process-1.0:=[profile?] <dev-haskell/process-1.7:=[profile?]
	>=dev-haskell/stm-2.4:=[profile?] <dev-haskell/stm-2.6:=[profile?]
	>=dev-lang/ghc-8.10.1:=
"
DEPEND="${RDEPEND}
	>=dev-haskell/cabal-3.2.0.0
"

src_configure() {
	haskell-cabal_src_configure \
		--flag=-examples \
		$(cabal_flag terminfo terminfo)
}
